function prompt_lambda17_precmd {
    _lambda17_git_info=""

    local head_ref=$(git rev-parse --abbrev-ref HEAD 2>/dev/null)
    if [ -z "$head_ref" ]; then
       return
    fi

    local is_dirty=""
    local git_status=$(git status --porcelain)
    if [ "$git_status" ]; then
        is_dirty="✗"
    fi

    _lambda17_git_info="%F{green}$head_ref%f%F{red}$is_dirty%f "
}

function prompt_lambda17_help {
    cat <<-END
	Prompt will display specified <letter> colored <fg_color> in space
	padded box with background color <bg_color>.

	<letter> by default is λ.
	<fg_color> by default is white.
	<bg_color> by default is red.

	prompt lambda17 <fg_color> <bg_color> <letter>
END
}

function prompt_lambda17_setup {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS

    prompt_opts=(cr percent subst)

    autoload -Uz add-zsh-hook
    add-zsh-hook precmd prompt_lambda17_precmd

    zstyle ':prezto:module:editor:info:keymap:primary' format ' '
    zstyle ':prezto:module:editor:info:keymap:alternate' format '·'

    # setting color
    PROMPT="%B%F{${1:-white}}%K{${2:-red}}"

    # setting keymap display (vi-mode) and <letter>.
    PROMPT="$PROMPT\${editor_info[keymap]:- }${3:-λ}\${editor_info[keymap]:- }"

    # setting path info
    PROMPT="$PROMPT%k%f %~%b"

    # setting git info
    PROMPT="$PROMPT \$_lambda17_git_info"

    RPROMPT=''
}

prompt_lambda17_setup "$@"
